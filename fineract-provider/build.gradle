/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements. See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership. The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
apply plugin: "org.springframework.boot"
apply plugin: "distribution"
apply plugin: "com.palantir.docker"
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: "openjpa"
// apply plugin: "com.ewerk.gradle.plugins.querydsl"

description = "Fineract"

tasks.withType(Tar){
    compression = Compression.GZIP
}

springBoot {
    buildInfo {
        properties {
            artifact = "fineract"
            version = "${project.version}"
            group = "${project.group}"
            name = "${project.description}"
        }
    }
}

openjpa {
    includes = ['**/AbstractPersistableCustom.class', '**/domain/*.class']
    enhance {
        enforcePropertyRestrictions true
    }
}

/*
querydsl {
    jpa = false
    springDataMongo = false
    querydslDefault = true
    aptOptions = [
        'querydsl.entityAccessors=true',
        'querydsl.useFields=false'
    ]
}
*/

dependencies {
    runtimeOnly "org.springframework.boot:spring-boot-devtools"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-activemq"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-quartz"
    implementation "org.springframework.boot:spring-boot-starter-jersey"
    implementation "org.glassfish.jersey.media:jersey-media-multipart"
    implementation "org.glassfish.jersey.media:jersey-media-sse"
    implementation "org.glassfish.jersey.media:jersey-media-jaxb"
    implementation "org.glassfish.jersey.ext:jersey-spring5"
    implementation "org.ow2.asm:asm-all"
    implementation "org.springframework.security.oauth:spring-security-oauth2"
    implementation "org.apache.openjpa:openjpa"
    implementation "com.squareup.retrofit2:retrofit"
    implementation "com.squareup.retrofit2:converter-gson"
    implementation "com.squareup.retrofit2:converter-jackson"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"
    implementation "com.google.code.gson:gson"
    implementation "com.google.guava:guava"
    implementation "joda-time:joda-time"
    implementation "org.apache.commons:commons-email"
    implementation "org.apache.commons:commons-lang3"
    implementation "mysql:mysql-connector-java"
    implementation "org.apache.poi:poi"
    implementation "org.apache.poi:poi-ooxml"
    implementation "org.apache.poi:poi-ooxml-schemas"
    implementation "org.apache.openjpa:openjpa-all"
    implementation "com.lowagie:itext"
    implementation "org.mnode.ical4j:ical4j"
    implementation "org.flywaydb:flyway-core"
    implementation "com.amazonaws:aws-java-sdk-s3"
    implementation "org.ehcache:ehcache"
    implementation "com.github.spullara.mustache.java:compiler"
    implementation "com.jayway.jsonpath:json-path"
    implementation "org.apache.tika:tika-core"
    implementation "io.swagger:swagger-jersey-jaxrs"
    implementation "javax.validation:validation-api"
    implementation "javax.cache:cache-api"
    implementation "org.apache.bval:org.apache.bval.bundle:2.0.2"
    implementation "joda-time:joda-time"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-sql"
    implementation "com.querydsl:querydsl-sql-spring"
    implementation "com.querydsl:querydsl-sql-codegen"
    implementation "com.querydsl:querydsl-codegen"
    implementation "com.querydsl:querydsl-jpa"
    implementation "com.querydsl:querydsl-jpa-codegen"
    implementation "com.querydsl:querydsl-apt"

    testCompile "junit:junit"
    testCompile "org.mockito:mockito-core"
    testCompile "com.jayway.restassured:rest-assured"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.testcontainers:mysql"
    // testCompile "org.testcontainers:localstack"
    // testCompile "com.avides.springboot.testcontainer:springboot-testcontainer-activemq"
    // testCompile "com.avides.springboot.testcontainer:springboot-testcontainer-mysql"

    integrationTestImplementation "junit:junit"
    integrationTestImplementation "com.jayway.restassured:rest-assured"
    integrationTestImplementation "com.google.code.gson:gson"
    integrationTestImplementation "com.google.guava:guava"
    integrationTestImplementation "org.apache.poi:poi"
    integrationTestImplementation "org.apache.poi:poi-ooxml"
    integrationTestImplementation "org.apache.poi:poi-ooxml-schemas"
    integrationTestImplementation "javax.ws.rs:javax.ws.rs-api"
    integrationTestImplementation "joda-time:joda-time"
    integrationTestImplementation "org.apache.openjpa:openjpa"
    integrationTestImplementation "mysql:mysql-connector-java"
}

tasks.distZip.enabled = false

def distributionsCopySpec = copySpec {
    into("fineract") {
        from "${buildDir}/${libsDirName}"
        exclude { FileTreeElement details ->
            details.file.name.endsWith(".original") || details.file.name.endsWith("-sources.jar")
        }
    }
}

bootJar {
    archiveFileName = "fineract-provider.jar"
}

task fatJarTar(type: Tar, dependsOn: "bootJar") { with distributionsCopySpec }

docker {
    name "fineract:latest"
    tag "build", "fineract:${project.version}"
    dependsOn fatJarTar
    files tasks.fatJarTar.outputs
}
